import "tfplan/v2" as tfplan
import "approved"
import "policy_summary"

param valid_actions default [
	["no-op"],
	["create"],
	["update"],
]

doc = {
	"allowed":   approved.awseks,
	"category":  "Infrastructure (IaaS)",
	"error":     " is not an allowed server type.",
	"file_name": "intel-aws-autoscaling-group-deny-unapproved-instance-types.sentinel",
	"name":      "kubernetes01",
	"md_url":    "https://github.com/intel/terraform-intel-aws-eks/blob/main/policies.md",
	"parameter": "instance_type",
	"provider":  "hashicorp/aws",
	"resource":  "aws_autoscaling_group",
	"violation": "The configured server type should use an Intel Xeon 3rd Generation Scalable processor (code-named Ice Lake)",
}

// Filter resources by type
all_resources = filter tfplan.resource_changes as _, rc {
	rc.type is doc.resource and
		rc.mode is "managed" and
		rc.change.actions in valid_actions
}


// Filter null values
null_handling = filter all_resources as _, r {
	all r.change.after.mixed_instances_policy as _, mixed_instance_policy {
		all mixed_instance_policy.launch_template as _, launch_template {
			all launch_template.override as _, override {
				override.instance_type is not null
			}
		}
	}
}

// Filter resources that violate a given condition
violators = filter null_handling as _, r {
	all r.change.after.mixed_instances_policy as _, mixed_instance_policy {
		all mixed_instance_policy.launch_template as _, launch_template {
			all launch_template.override as _, override {
				override.instance_type not in doc.allowed
			}
		}
	}
}

// Build a summary report
summaryreport = {
	"name": doc.name,
	"violations": map violators as _, violation {
		{
			"name":    violation.name,
			"address": violation.address,
			"type":    violation.type,
			"message": violation.name + doc.error,
		}
	},
}

main = rule {
	policy_summary.summary(summaryreport, doc)
}
