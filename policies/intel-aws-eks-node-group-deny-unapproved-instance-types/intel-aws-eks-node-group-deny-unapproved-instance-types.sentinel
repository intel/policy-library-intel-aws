import "tfplan/v2" as tfplan
import "approved"

param valid_actions default [
	["no-op"],
	["create"],
	["update"],
]

summary = func(input) {
	result = false
	if input.violations is empty {
		return true
	} else {
		log(input)
	}
	return result
}

log = func(input) {
	header(input)
	violations(input)
	return null
}

header = func(input) {
	print(
		"\t========================================================================\n",
		"\t                    _       _       _ \n",
		"\t                   (_)     | |     | |\n",
		"\t                    _ _ __ | |_ ___| |\n",
		"\t                   | | '_ \\| __/ _ \\ |\n",
		"\t                   | | | | | ||  __/ |\n",
		"\t                   |_|_| |_|\\__\\___|_|\n",
		"\t\n",
		"\t========================================================================\n",
		"\tName        :" + "intel-aws-eks-node-group-deny-unapproved-instance-types.sentinel\n",
		"\tCategory    :" + "Platform (PaaS)\n",
		"\tProvider    :" + "hashicorp/aws\n",
		"\tResource    :" + "aws_eks_node_group\n",
		"\tParameter   :" + "instance_types\n",
		"\tCheck       :" + "instance_type does not contain\n",
		"\t\n",
		"\tFor a list of allowed instance types see:\n",
		"\thttps://github.com/intel/terraform-intel-aws-eks/blob/main/policies.md\n",
	)
	return null
}

violations = func(input) {
	print(
		"\t========================================================================\n",
		"\tRESOURCE VIOLATIONS\n",
		"\tThe configured server type should use an Intel Xeon 3rd Generation Scalable processor (code-named Ice Lake)\n",
		"\t========================================================================\t",
	)
	for input.violations as violation {
		print(
			"\t",
			"name       :" + violation.name + "\n\t",
			"type       :" + violation.type + "\n\t",
			"address    :" + violation.address + "\n\t",
			"message    :" + violation.message + "\t\n",
			"\t------------------------------------------------------------------------\t",
		)
	}
	if (input.violations is not empty) {
		print(
			"\t",
			"Resources out of compliance: " + string(length(input.violations)) + "\n",
			"\t------------------------------------------------------------------------\t",
		)
	}
	return null
}

// Allowed server resource types
allowed_server_types = approved.awseks

doc = {
	"id":       "kubernetes01",
	"resource": "aws_eks_node_group",
	"name":     "Server Type must be one of the approved instances",
}

// Filter resources by type
all_resources = filter tfplan.resource_changes as _, rc {
	rc.type is doc.resource and
		rc.mode is "managed" and
		rc.change.actions in valid_actions
}

// Filter resources that violate a given condition
violators = filter all_resources as _, r {
	all r.change.after.instance_types as _, instance_types {
		instance_types not in allowed_server_types
	}
}

// Build a summary report
summaryreport = {
	"id":   doc.id,
	"name": doc.name,
	"violations": map violators as _, violation {
		{
			"name":    violation.name,
			"address": violation.address,
			"type":    violation.type,
			"message": violation.name + " must be configured with an allowed server type.",
		}
	},
}

main = rule {
	summary(summaryreport)
}
