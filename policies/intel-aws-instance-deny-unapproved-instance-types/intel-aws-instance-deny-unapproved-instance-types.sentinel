import "tfplan/v2" as tfplan
import "approved"
import "policy_summary"

param valid_actions default [
	["no-op"],
	["create"],
	["update"],
]

doc = {
	"allowed":   approved.awsinstance,
	"category":  "Infrastructure (IaaS)",
	"error":     " is not an allowed server type.",
	"file_name": "intel-aws-instance-deny-unapproved-instance-types.sentinel",
	"name":      "awsvm01",
	"md_url":    "https://github.com/intel/terraform-intel-aws-vm/blob/main/policies.md\n",
	"parameter": "instance_type",
	"provider":  "hashicorp/aws",
	"resource":  "aws_instance",
	"violation": "The configured server type should use an Intel Xeon 3rd or 4th Generation Scalable processor (code-named Ice Lake and Sapphire Rapids)",
}

// Filter resources by type
all_resources = filter tfplan.resource_changes as _, rc {
	rc.type is doc.resource and
		rc.mode is "managed" and
		rc.change.actions in valid_actions
}

// Filter resources that violate a given condition
violators = filter all_resources as _, r {
	r.change.after.instance_type else [] not in doc.allowed
}

// Build a summary report
summaryreport = {
	"name": doc.name,
	"violations": map violators as _, violation {
		{
			"name":    violation.name,
			"address": violation.address,
			"type":    violation.type,
			"message": violation.change.after.instance_type + doc.error,
		}
	},
}

main = rule {
	policy_summary.summary(summaryreport, doc)
}
