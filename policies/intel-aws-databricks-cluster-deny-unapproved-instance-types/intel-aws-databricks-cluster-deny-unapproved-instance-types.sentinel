import "tfplan/v2" as tfplan
import "approved"
import "policy_summary"

param valid_actions default [
	["no-op"],
	["create"],
	["update"],
]

doc = {
	"allowed":   approved.awsdbx,
	"category":  "Infrastructure (IaaS)",
	"error":     " is not an allowed server type.",
	"file_name": "intel-aws-databricks-cluster-deny-unapproved-instance-types.sentinel",
	"name":      "dbxoptimizedcluster",
	"md_url":    "https://github.com/intel/terraform-intel-aws-databricks/blob/main/policies.md",
	"parameter": "node_type",
	"provider":  "databricks/databricks",
	"resource":  "databricks_cluster",
	"violation": "The configured server type should use an Intel Xeon 3rd Generation Scalable processor (code-named Ice Lake)",
}

// Filter resources by type
all_resources = filter tfplan.resource_changes as _, rc {
	rc.type is doc.resource and
		rc.mode is "managed" and
		rc.change.actions in valid_actions
}

// Filter null values
null_handling = filter all_resources as _, r {
	r.change.after.node_type_id is not null
}

// Filter resources that violate a given condition
violators = filter null_handling as _, r {
	r.change.after.node_type_id not in doc.allowed
}

// Build a summary report
summaryreport = {
	"name": doc.name,
	"violations": map violators as _, violation {
		{
			"name":    violation.name,
			"address": violation.address,
			"type":    violation.type,
			"message": violation.name + doc.error,
		}
	},
}

main = rule {
	policy_summary.summary(summaryreport, doc)
}
