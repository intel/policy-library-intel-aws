import "tfplan/v2" as tfplan
import "approved"
import "policy_summary"

param valid_actions default [
	["no-op"],
	["create"],
	["update"],
]

doc = {
	"allowed":   approved.awsrdscluster,
	"category":  "Infrastructure (IaaS)",
	"error":     " uses a non-approved instance class",
	"file_name": "intel-aws-rds-cluster-deny-unapproved-instance-types.sentinel",
	"name":      "intel-aws-rds-cluster-deny-unapproved-instance-types",
	"md_url":    "https://github.com/intel/policy-library-intel-aws/blob/main/policies/policies.md",
	"parameter": "db_cluster_instance_class",
	"provider":  "hashicorp/aws",
	"resource":  "aws_rds_cluster",
	"violation": "The configured server type should use an Intel Xeon 3rd Generation Scalable processor (code-named Ice Lake)",
}

// Filter resources by type
all_resources = filter tfplan.resource_changes as _, rc {
	rc.type is doc.resource and
		rc.mode is "managed" and
		rc.change.actions in valid_actions
}

// Filter resources that violate a given condition
violators = filter all_resources as _, r {
	r.change.after.db_cluster_instance_class else [] not in doc.allowed
}

// Build a summary report
summaryreport = {
	"name": doc.name,
	"violations": map violators as _, violation {
		{
			"name":    violation.name,
			"address": violation.address,
			"type":    violation.type,
			"message": violation.name + doc.error,
		}
	},
}

main = rule {
	policy_summary.summary(summaryreport, doc)
}
